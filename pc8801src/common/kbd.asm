;==========================================================================
; MUSICLALF Ver.1.0Å`1.2ã§í  ÉvÉçÉOÉâÉÄÉ\Å[ÉX
; ÉtÉ@ÉCÉãñº : kbd.asm
; ã@î\ : FMâπåπâπêFÉGÉfÉBÉ^
; PROGRAMED BY YUZO KOSHIRO
;==========================================================================
; ÉwÉbÉ_ï“èW/É\Å[ÉXèCê≥ : @mucom88
;==========================================================================
	
	
	ORG 9A00H
	
VOICELOC:	EQU 	0*256+40
CURSOR:	EQU	0EF86H
CUR_ON:	EQU	04290H
MSTART:	EQU	0B000H
MST:	EQU	MSTART+3*6
AKYOFF:	EQU	MST+3*2
PSGOUT:	EQU	MST+3*7
WKGET:	EQU	MST+3*8
STVOL:	EQU	MST+3*9
	
OTODAT: EQU 	06000H
	
MWRITE:		EQU	9000H
MWRIT2:		EQU	MWRITE+3
ERRT:		EQU	MWRIT2+3
ERRORSN:	EQU	ERRT+3
ERRORIF:	EQU	ERRORSN+3
ERRORNF:	EQU	ERRORIF+3
ERRORFN:	EQU	ERRORNF+3
ERRORVF:	EQU	ERRORFN+3
ERROROO:	EQU	ERRORVF+3
ERRORND:	EQU	ERROROO+3
ERRORRJ:	EQU	ERRORND+3
STTONE:		EQU	ERRORRJ+3
STLIZM:		EQU	STTONE+3
REDATA:		EQU	STLIZM+3
MULT:		EQU	REDATA+3
DIV:		EQU	MULT+3
HEXDEC:		EQU	DIV+3
HEXPRT:		EQU	HEXDEC+3
ROM:		EQU	HEXPRT+3
RAM:		EQU	ROM+3
FMCOMC:		EQU	RAM+3
T_RST:		EQU	FMCOMC+3
ERRORNE:	EQU	T_RST+3
ERRORDC:	EQU	ERRORNE+3
ERRORML:	EQU	ERRORDC+3
MCMP:		EQU	ERRORML+3
ERRORVO:	EQU	MCMP+3
	
SMON:	EQU	0DE00H
CONVERT:EQU	SMON+3*2
CONVERT2:EQU	SMON+3*3
GETPARA:EQU	SMON+3*4
OPEX:	EQU	SMON+3*5
OTOWK:	EQU	8B00H+60H+60H	;25BYTE√ﬁ∞¿∂ﬁ  ≤Ÿƒ∫€
PARAM:	EQU	OTOWK+32	;38BYTE√ﬁ∞¿∂ﬁ ≤Ÿƒ∫€
	
	
; ***   INITIARIZE   ***
	
ST:
	CALL	CLS
	CALL	BACK
	
	LD	A,(0E6C2H)
	SET	3,A
	LD	(0E6C2H),A
	OUT	(31H),A
	
	CALL	CUR_ON
	CALL 	KEYSCOFF
	
	CALL	W2INIT
	
	LD 	DE,0000H
	LD 	HL,MSG001
	CALL 	DISPLAY
	
	LD	A,4
	LD	(OCTAVE),A
	CALL	OCTP
	CALL 	STENV
	
	LD 	BC,1*256+11
VSETL:
	PUSH	BC
	CALL	WKGET
	LD	(IX+6),10
	CALL 	STVOL
	POP	BC
	INC	B
	DEC	C
	JR	NZ,VSETL
	
	LD 	A,(ONSHOKU)
	LD 	L,A
	LD 	H,0
	CALL 	HEXDEC
	LD 	DE,VOICELOC
	CALL 	DISPLAY
	
	LD 	DE,0*256+50
	LD	HL,MSG002
	CALL 	DISPLAY
	
	CALL	VFILE		; VOICE FILE ¶ ÀÆ≥ºﬁ
	CALL	INIT1
	CALL	USEVIC
	CALL	OTOPST
	CALL	SCP4
	
; ***   MAIN   ***
	
START:
	LD	A,(EDITFG)
	OR	A
	JR	Z,START2
	
	IN	A,(9)
	BIT	7,A	; ESC KEY
	CALL	Z,EDITCH2
	
START2:
	LD 	HL,OCTAVE
	PUSH	HL
	IN 	A,(0AH)
	CALL	KEYSTOP
			 ;BIT 5,A
	CALL 	OCTVDOWN
	POP	HL
	IN 	A,(1)
			 ;BIT 3,A
	CALL 	OCTVUP
	
	LD	A,(EDITFG)
	OR	A
	JR	NZ,ST2
	
; ---   EDIT ¡≠≥≈◊ ºÆÿ º≈≤   ---
	
	LD 	HL,ONSHOKU
	PUSH	HL
	IN 	A,(0BH)
			 ;BIT 1,A
	CALL 	OTODOWN
	POP	HL
	IN 	A,(0BH)
			 ;BIT 0,A
	CALL 	OTOUP
	
	IN	A,(0AH)
	BIT	3,A
	CALL	Z,HELP
	
	IN	A,(8)
	BIT	7,A
	JR	NZ,ST3		; CTRL∑∞ ∂ﬁ µª⁄√≤≈π⁄ ﬁ ∫œ›ƒﬁ ∆≠≥ÿÆ∏ ¶ —º
	
	IN	A,(2)
	BIT	4,A		;D
	CALL	Z,DELETE
	IN	A,(2)
	BIT	5,A		;E
	CALL	Z,EDITCH
	IN	A,(4)
	BIT	3,A		;S
	CALL	Z,SYSTEM
	IN	A,(4)		;Q
	BIT	1,A
	JR	Z,LOOPEND
	IN	A,(3)
	BIT	4,A       	;L
	CALL	Z,LOADF
	
	JP	ST3
	
; **	HELP FILE	**
	
HELP:
	LD	HL,MSG035
	CALL	WINDOW2
HE2:
	IN	A,(9)
	BIT	7,A
	JR	NZ,HE2
	CALL	W2INIT
	RET
	
	
; ---   EDIT ¡≠≥ … ºÆÿ   ---
	
ST2:
	CALL	EDIT
	
; ---   ∑∞ ∆≠≥ÿÆ∏ ¡™Ø∏   ---
	
ST3:
	LD 	B,4
	LD 	C,1
	
PORTCHECK:
	
	INC	C
	IN 	A,(C)
	CP 	0FFH
	JP 	NZ,BITCHECK
	
	DJNZ 	PORTCHECK
	
	CALL 	AKYOFF	;KEYOFF
;END:
	JP 	START
	
LOOPEND:
	CALL	KEYSCON
	CALL	AKYOFF
	LD	A,(0E6C2H)
	RES	3,A
	LD	(0E6C2H),A
	OUT	(31H),A
	CALL	CLS
	RET
	
; ***   INITIARIZE 1   ***
	
INIT1:
	LD	HL,MSG004
	LD	DE,030BH
	CALL 	DISPLAY
	
	LD	HL,MSG005
	LD	DE,0500H
	LD	B,10
INIL1:
	PUSH	BC
	PUSH	DE
	PUSH	HL
	CALL 	DISPLAY
	POP	HL
	LD	DE,17
	ADD	HL,DE
	POP	DE
	INC	D
	INC	D
	POP	BC
	DJNZ	INIL1
	
	LD	HL,MSG006
	LD	DE,0100H
	CALL	DISPLAY
	
	LD	HL,MSG031
	LD	DE,8*256+39
	CALL	DISPLAY
	LD	HL,MSG032
	LD	DE,13*256+39
	CALL	DISPLAY
	LD	HL,MSG033
	LD	DE,18*256+39
	CALL	DISPLAY
	LD	HL,MSG034
	LD	DE,23*256+39
	CALL	DISPLAY
	
	RET
	
; ***   SETLECT MODE   ***
	
RAMI:
	DI
	CALL	RAM
	RET
ROMI:
	CALL	ROM
	EI
	RET
	
; ***   DELETE FILE   ***
	
DELETE:
	CALL	BEEP
	LD	HL,MSG028
	CALL	WINDOW1
	CALL	GETSUJI
	LD	A,L
	CALL	GETADR
	LD	(HL),0
	RET
	
; ***   SYSTEM MODE   ***
	
SYSTEM:
	CALL	BEEP
	
	LD	HL,0FFFFH
	CALL	WAIT
	
	LD	HL,MSG024
	CALL	WINDOW2
SYS1:
	IN	A,(4)
	BIT	3,A
	JR	Z,SYS3
	
	IN	A,(3)
	BIT     4,A
	JR      Z,SYS2
	
	CALL	ESCCHECK
	JR	C,SYS4
	
	JP	SYS1
SYS2:             		; DISK LOAD ROUTINE
	LD	DE,21*256+56
	LD	HL,MSG026
	CALL	DISPLAY
	LD	HL,LDATA
	CALL	0EEBFH
	JP	SYS4
	
LDATA:
	DB	'"voice.dat"',00H
SYS3:				; DISK SAVE ROUTINE
	LD	DE,21*256+56
	LD	HL,MSG025
	CALL	DISPLAY
	LD	HL,SDATA
	CALL	0EEC2H
	JP	SYS4
SDATA:
	DB	'"voice.dat",&H6000,&H2000',00H
	
SYS4:
	CALL	W2INIT
	CALL	BEEP
	RET
	
W2INIT:
	LD	HL,MSG022
	CALL	WINDOW2
	RET
	
; ***   BEEP SOUND   ***
	
BEEP:
	PUSH	BC
	LD	B,255
BEEP0:
	PUSH	BC
	LD	B,20
BEEP2:
	LD	A,(0E6C1H)
	OR	10000000B
	OUT	(40H),A
BEEP3:
	DJNZ	BEEP2
	LD	A,(0E6C1H)
	OUT	(40H),A
	LD	B,20
BEEP4:
	DJNZ	BEEP4
	
	POP	BC
	DJNZ	BEEP0
	
	POP	BC
	RET
	
	
; ***   ESC KEY CHECK   ***
	
ESCCHECK:
	IN	A,(9)
	BIT	7,A
	JR	Z,STCK2
	AND	A
	RET
STCK2:
	SCF   		; µª⁄√≤Ÿ
	RET
	
; ***   TO EDIT MODE   ***
	
EDITCH:
	CALL	BEEP
	
	LD	A,255
	LD	(EDITFG),A
	
	LD	DE,14*256+6
	LD	(CURSOR),DE
	
	XOR	A
	LD      (INPCO),A
	
	LD	DE,1*256+25
	LD	HL,MSG007
	CALL	DISPLAY
	LD	HL,MSG040
	CALL	WINDOW2
	RET
	
EDITCH2:
	CALL	BEEP
	LD	HL,MSG018
	CALL	WINDOW2
EDCH1:
	IN	A,(2)
	LD	HL,TEN0BUF
	CALL	KEY_CHECK
	BIT	5,A		; E
	JR	Z,EDCH4
	BIT	2,A		; B
	JR	Z,BACKUP
	
	IN	A,(4)
	LD	HL,TEN1BUF
	CALL	KEY_CHECK
	BIT	1,A 		; Q
	JP	Z,EDCH5
	BIT	2,A
	JR	Z,EDCH3		; R
	
	JP	EDCH1
	
	
BACKUP:
	LD	HL,OTODAT
	LD	(OTOADR),HL
	CALL	OTOPST
	CALL	EDITCH
	CALL	W2INIT
	RET
	
EDCH3:
	CALL	BEEP
	LD	DE,(OTOADR)
	LD	HL,OTOWK
	LD	BC,26
	LDIR
	
	LD	HL,MSG021
	CALL	WINDOW1
	LD	A,(ONSHOKU)
	LD	L,A
	LD	H,0
	CALL	HEXDEC
	LD	DE,3*256+60
	INC	HL
	INC	HL
	CALL	DISPLAY
	
	JP	EDCH52
	
EDCH4:
	CALL	BEEP
	CALL	RAMI
	LD	HL,OTOWK
	LD	DE,OTODAT
	LD	BC,32
	LDIR
	CALL	ROMI
	
	LD	HL,MSG029
	CALL	WINDOW1
	CALL	GETSUJI
	LD	A,L
	LD	(EDCH41+1),A
	OR	A
	JR	Z,EDCH42
	CALL	GETADR
	
	PUSH	HL
	LD	HL,MSG020
	CALL	WINDOW1
	CALL	LININP		; LINE INPUT
	POP	HL
	JP	C,EDCH5
EDCH41:
	LD	C,0
	JP	EDCH43
EDCH42:
	LD	HL,MSG020
	CALL	WINDOW1
	CALL	LININP		; LINE INPUT
	JR	C,EDCH5
	CALL	USEVIC
EDCH43:
	PUSH	BC
	PUSH	HL
	EX	DE,HL     	; ∂ﬁ“›ºﬁÆ≥ … µ›ºÆ∏√ﬁ∞¿ ¶
	LD	HL,OTOWK        ;
	LD	BC,32           ; ±∑ ¥ÿ± ∆ ƒ≥€∏
	LDIR                    ;
	POP	HL
	
	LD	DE,26
	ADD	HL,DE
	EX	DE,HL
	LD	HL,INPBUF
	LD	BC,6
	LDIR			; FILE NAME ¶ µ›ºÆ∏Ãß≤Ÿ Õ √›ø≥
	
	LD	HL,MSG021
	CALL	WINDOW1
	
	POP	BC
	LD	L,C
	LD	H,0
	CALL	HEXDEC
	LD	DE,3*256+60
	INC	HL
	INC	HL
	CALL	DISPLAY
	JP	EDCH52
EDCH5:
	CALL	BEEP
EDCH52:
	XOR	A
	LD	(EDITFG),A
	LD	DE,0101H
	LD	(CURSOR),DE
	CALL	CUR_ON
	LD	DE,1*256+25
	LD	HL,MSG008
	CALL	DISPLAY
	CALL    W2INIT
	CALL	USEVIC
	RET
	
	
; ***   LINE INPUT SUB ROUTINE (6 CODE MAX)  ***
	
LININP:
	LD	HL,60000
	CALL	WAIT
	CALL	KEYSCON
	LD	DE,69*256+10
	LD	(CURSOR),DE
LININ2:
	CALL	3583H	; ∑∞Œﬁ∞ºﬁ 1”ºﬁ œ¡
	
	CP	0DH     ; RETURN KEY ?
	JR      Z,LININ7
	CP	1BH	; ESC KEY?
	JR	Z,LININ8
	CP	7FH	; BACK SPACE
	JR	Z,LININ9
	CP	1CH
	JR	C,LININ2
	CP	1EH
	JR	Z,LININ2
	CP	1FH
	JR	Z,LININ2
	
	CALL	3E0DH	; 1 ”ºﬁ º≠¬ÿÆ∏
	
	LD	A,(CURSOR+1)
	CP	75
	JR	NZ,LININ3
	DEC	A
	LD	(CURSOR+1),A
	JP	LININ2
LININ3:
	CP	68
	JR	NZ,LININ2
	INC	A
	LD	(CURSOR+1),A
	JP	LININ2
LININ7:
	LD	HL,0F3C8H+120*9+68
	LD	DE,INPBUF
	LD	BC,6
	LDIR
	CALL	KEYSCOFF
	RET
LININ8:
	CALL	KEYSCOFF
	SCF		; BREAK FLAG
	RET
LININ9:
	LD	A,(CURSOR+1)
	CP	69
	JR	Z,LININ2
	DEC	A
	LD	(CURSOR+1),A
	JR	LININ2
	
INPBUF:
	DB	0,0,0,0,0,0,0
	
; ***   WAIT TIME   ***
	
;	IN:HL<=TIME
	
WAIT:
	LD	A,H
	OR	L
	RET	Z
	DEC	HL
	JP	WAIT
	
; ***   OS ∂◊ … ∑∞Ω∑¨› ON/OFF   ***
	
KEYSCON:
	XOR	A
	LD	(0E6CDH),A
	RET
	
KEYSCOFF:
	LD	A,255
	LD	(0E6CDH),A
	RET
	
EDIT:
	IN	A,(8)
	AND	00000110B
	RLCA
	RLCA
	LD	C,A
	IN	A,(0AH)
	AND	00000110B
	OR	C
	
	LD	(EDIT1+1),A
	LD	HL,CURBUF
	CALL	KEY_CHECK
	JP	C,EDIT2
EDIT1:
	LD	B,0
	LD	DE,(CURSOR)
	
	BIT	4,B
	JR	NZ,ED2
	
	LD	A,D
	CP	29
	JR	NC,ED2
	
	LD	A,D
	ADD	A,5
	LD	D,A
	JR	ED5
	
ED2:
	BIT	3,B
	JR	NZ,ED3
	
	LD	A,E
	CP	6
	JR	Z,ED3
	
	DEC	E               ; UP
	DEC	E
	JR	ED5
ED3:
	BIT	2,B
	JR	NZ,ED4
	
	LD	A,D
	CP	15
	JR	C,ED4
	
	LD	A,D
	SUB	5
	LD	D,A
	JR	ED5
ED4:
	BIT	1,B
	JR	NZ,ED52
	
	LD	A,E
	CP	24
	JR	Z,ED52
	INC	E		; DOWN
	INC	E
	
ED5:
	PUSH	DE
	CALL	SCP4
	POP	DE
ED52:
	LD	(CURSOR),DE
	
; --	SEARCH	ROLLKEY	--
	
EDIT2:
	LD	HL,(CURSOR)
	CALL	429DH
	DEC	HL
	DEC	HL
	PUSH	HL
	CALL	CVR1
	PUSH	AF
	
	IN 	A,(0BH)
	LD	HL,ROLLBUF
	CALL	KEY_CHECK
	JR	Z,EDIT21
	JR	C,EDIT21
	
	BIT	1,A	; DOWN
	JR	Z,EDIT24
	BIT	0,A	; UP
	JR	Z,EDIT22
EDIT21:
	POP	AF
	POP	HL
	JR	EDIT3
EDIT22:
	POP	AF
	INC	A
EDIT23:
	LD	L,A
	LD	H,0
	CALL	HEXDEC
	INC	HL
	INC	HL
	POP	DE
	LDI
	LDI
	LDI
	JP	SCP4
EDIT24:
	POP	AF
	OR	A
	JR	Z,EDIT23
	DEC	A
	JR	EDIT23
	
; ---   SEARCH TENKEY   ---
	
EDIT3:
	IN	A,(0)
	LD	HL,TEN0BUF
	CALL	KEY_CHECK
	JR	Z,EDIT32
	JR	NC,TEN1
	RET
EDIT32:
	LD	HL,TEN0BUF
	CALL	KEY_CHECK 	; ≤¡ƒﬁ  ﬁØÃß ¶ ∏ÿ±
	
	IN	A,(1)
	LD	HL,TEN1BUF
	CALL	KEY_CHECK
	JR	Z,EDIT33
	JR	NC,TEN9
	RET
EDIT33:
	LD	HL,TEN1BUF
	CALL	KEY_CHECK 	; ≤¡ƒﬁ  ﬁØÃß ¶ ∏ÿ±
	RET
TEN1:
	BIT	0,A
	JR	NZ,TEN2
	LD	C,'0'
	CALL	SCPRNT
	RET
TEN2:
	BIT	1,A
	JR	NZ,TEN3
	LD	C,'1'
	CALL	SCPRNT
	RET
TEN3:
	BIT	2,A
	JR	NZ,TEN4
	LD      C,'2'
	CALL	SCPRNT
	RET
TEN4:
	BIT	3,A
	JR	NZ,TEN5
	LD	C,'3'
	CALL	SCPRNT
	RET
TEN5:
	BIT	4,A
	JR	NZ,TEN6
	LD	C,'4'
	CALL	SCPRNT
	RET
TEN6:
	BIT	5,A
	JR	NZ,TEN7
	LD	C,'5'
	CALL	SCPRNT
	RET
TEN7:
	BIT	6,A
	JR	NZ,TEN8
	LD	C,'6'
	CALL	SCPRNT
	RET
TEN8:
	BIT	7,A
	JR	NZ,TEN9
	LD	C,'7'
	CALL	SCPRNT
	RET
TEN9:
	BIT	0,A
	JR	NZ,TENA
	LD	C,'8'
	CALL	SCPRNT
	RET
TENA:
	BIT	1,A
	RET	NZ
	LD	C,'9'
	CALL	SCPRNT
	RET
	
SCPRNT:
	LD	HL,(CURSOR)
	PUSH	BC
	CALL	429DH
	
	LD	A,(INPCO)
	PUSH	AF
	OR	A
	JR	NZ,SCP1
	LD	(HL),'*'
SCP1:
	DEC	HL
	LD	E,L
	LD	D,H
	DEC	DE
	LDI
	LDI
	DEC	HL
	POP	AF
	POP	BC
	LD	(HL),C
	
	CP	2
	JR	NC,SCP3
	INC	A
	LD	(INPCO),A
	RET
SCP3:
	XOR	A
	LD	(INPCO),A
SCP4:
	CALL	CVDAT
	CALL	GETPARA
	CALL	STENV
	CALL	GRAPH
	CALL	PUTALGO
	RET
	
; ***   SEARCH USED VOICE   ***
	
;	EXIT:	C <= FREE VOICE's TOP NUMBER
;		HL<= NEXT FREE VOICE ADR
	
USEVIC:
	CALL	RAMI
	
	LD	HL,OTODAT
	LD	DE,32
	LD	BC,255*256
USEV1:
	LD	A,(HL)
	OR	A
	JR	Z,USEV2
	ADD	HL,DE
	INC	C
	DJNZ	USEV1
	
USEV2:
	PUSH	BC
	DEC	C
	LD	L,C
	LD	H,0
	CALL	HEXDEC
	LD      DE,1*256+66
	INC	HL
	INC	HL
	CALL	DISPLAY
	
	LD	DE,1*256+51
	LD	HL,MSG019
	CALL	DISPLAY
	CALL	ROMI
	
	POP	BC
	LD	A,C
	CALL    GETADR
	
	RET
	
; ***   µ›ºÆ∏ ∂ﬁ ∂∏…≥ª⁄√≤Ÿ ±ƒﬁ⁄Ω ¶ ”ƒ“Ÿ   ***
	
;	IN:	A <= NUMBER
;	EXIT:	HL<= ADR
	
GETADR:
	PUSH	DE
	LD	L,A
	LD	H,0
	ADD	HL,HL
	ADD	HL,HL
	ADD	HL,HL
	ADD	HL,HL
	ADD	HL,HL
	LD	DE,OTODAT
	ADD	HL,DE
	POP	DE
	RET
	
; ***   µ›ºÆ∏  ﬂ◊“∞¿ ÀÆ≥ºﬁ   ***
	
OTOPST:
	PUSH	HL
	PUSH	DE
	PUSH	BC
	
	CALL 	RAMI
	
	LD	HL,(OTOADR)
	LD	DE,OTOWK
	LD	BC,32
	LDIR
	
	LD	HL,(OTOADR)
	CALL 	CONVERT2 		; µ›ºÆ∏Ãß≤Ÿ ¶ BASIC TYPE ∆ Õ›∂›
	
	LD	HL,PARAM
	LD	B,10
	CALL 	PFILE
	
	CALL	GRAPH
	CALL	PUTALGO
	
	CALL 	ROMI
	
	POP	BC
	POP 	DE
	POP	HL
	
	RET
	
; ***   µ›ºÆ∏ ∏ﬁ◊Ã ¶ ÀÆ≥ºﬁ   ***
	
GRAPH:
	LD	C,5EH
	CALL	GCLS
	LD	C,5DH
	CALL	GCLS
	
	LD	IX,PARAM
	LD	A,72
	CALL	GRAPH2
	LD	IX,PARAM+1
	LD	A,112
	CALL	GRAPH2
	LD	IX,PARAM+2
	LD	A,152
	CALL	GRAPH2
	LD	IX,PARAM+3
	LD	A,152+40
	CALL	GRAPH2
	
	RET
	
GRAPH2:
	LD	(LINEMEM+2),A 	; SET YMAX
	LD	(LINEMEM+18),A
	SUB	32
	LD	(LINEMEM+6),A
	
	LD	C,(IX)     	; AR
	LD	A,32
	SUB	C
	LD	C,A
	
	LD	DE,8*38
	ADD	A,E
	LD	E,A
	ADC	A,D
	SUB	E
	LD	D,A
	LD	(LINEMEM+4),DE
	
	LD	A,32
	LD	C,(IX+4)        ; DR
	SUB	C
	ADD	A,E
	LD	E,A
	ADC 	A,D
	SUB	E
	LD	D,A
	LD	(LINEMEM+8),DE
	
	LD	A,32
	ADD	A,E
	LD	E,A
	ADC 	A,D
	SUB	E
	LD	D,A
	LD	(LINEMEM+12),DE
	
	PUSH	DE
	
	LD	A,(IX+16)	; SL
	ADD	A,A             ; *2
	LD	C,A
	LD	A,(LINEMEM+6)
	ADD	A,C
	LD	(LINEMEM+10),A
	LD	C,A
	
	LD      A,(IX+8)        ; SR
	SRL	A               ; *2/1
	ADD	A,C
	LD	C,A
	LD	A,(LINEMEM+2)	; YMAX
	CP	C
	LD	A,C
	JR 	NC,LSKIP1
	LD	A,(LINEMEM+2)
	
LSKIP1:
	LD	(LINEMEM+14),A
	
	POP	DE
	LD	A,(IX+12)     	; RR
	ADD	A,A
	
	LD	C,A
	LD	A,31
	SUB	C
	
	ADD	A,E
	LD	E,A
	ADC 	A,D
	SUB	E
	LD	D,A
	LD	(LINEMEM+16),DE
	
	LD	C,05EH
	LD	HL,LINEMEM
	CALL	LINE
	
	LD	C,05EH
	LD	HL,LINEMEM+4
	CALL	LINE
	
	LD	C,05EH
	LD	HL,LINEMEM+8
	CALL	LINE
	
	LD	C,05DH
	LD	HL,LINEMEM+12
	CALL	LINE
	
	RET
	
	
LINEMEM:
	DW	8*38,52,8*38,20,8*38,52,8*38,52,8*38,52
LINE_X:
	DW	0
YMAX:
	DB	0
	
GCLS:
	DI
	OUT	(C),A
	LD      HL,0C000H+40*80+38
	LD	B,153
	LD      D,H
	LD	E,L
	INC	DE
GCLS2:
	PUSH	BC
	LD	(HL),0
	LD	BC,15
	LDIR
	LD	BC,80-15
	ADD	HL,BC
	EX	DE,HL
	ADD	HL,BC
	EX	DE,HL
	POP BC
	DJNZ	GCLS2
	OUT	(5FH),A
	EI			; ∏ﬁ◊Ã πº¿
	RET
	
; ***   PUT ALGOLIZM   ***
	
PUTALGO:
	
	CALL	ALGOCLS
	LD	A,(PARAM+38)   	; ALGO
	
	LD	HL,ALGOTABL
	ADD	A,A
	ADD	A,L
	LD	L,A
	ADC	A,H
	SUB	L
	LD	H,A
	
	LD      A,(HL)
	INC	HL
	LD	H,(HL)
	LD	L,A
	
	LD	DE,5*256+54
	LD	B,4
	CALL	DISPBOX
	RET
	
ALGOCLS:
	LD	DE,5*256+54
	LD	HL,MSG008
	LD	B,4
	CALL	DISPBOX
	RET
	
ALGOTABL:
	DW	MSG010
	DW	MSG011
	DW	MSG012
	DW	MSG013
	DW	MSG014
	DW	MSG015
	DW	MSG016
	DW	MSG017
	
; ***   PRINT VOICE FILE   ***
	
;	IN:  B  <= DATA ¡Æ≥
;            HL <= WORK ADR
	
PFILE:
	LD	DE,050BH 	; Y=4,X=11
	LD	(LOCATE),DE
PF0:
	PUSH	BC
	LD	B,4
PF1:
	PUSH	BC
	PUSH	HL
	
	LD  	L,(HL)
	LD	H,0
	
	PUSH	DE
	CALL 	HEXDEC
	INC	HL
	INC	HL
	
	POP 	DE
	PUSH	DE
	CALL	DISPLAY
	POP	DE
	
	LD	A,5
	ADD	A,E
	LD	E,A
	
	POP 	HL
	INC	HL
	POP	BC
	DJNZ    PF1
	
	LD	DE,(LOCATE)
	INC	D
	INC	D
	LD	(LOCATE),DE
	
	POP	BC
	DJNZ	PF0
	
	LD	DE,23*256+16
	LD	HL,MSG041
	CALL	DISPLAY
	LD	DE,23*256+26
	LD	HL,MSG041
	CALL	DISPLAY
	RET
	
	
	
; ***   ∂ﬁ“›ºﬁÆ≥ … √ﬁ∞¿ ¶ PARAM Õ √›ø≥   ***
	
CVDAT:
	LD	HL,0F3C8H+5*120+11
	LD	DE,PARAM
	LD	B,10
	XOR	A
	LD	(REEROR),A
CVDAT0:
	PUSH	BC
	
	PUSH	HL
	POP	IX
	LD	BC,21
	ADD	IX,BC
	
	LD	B,4
	
CVDAT1:
	PUSH	BC
	PUSH	DE
	PUSH	HL
	
	CALL	CVR1
	
	LD	C,A
	PUSH    IX
	POP	HL
	
	CALL	CVR1
	CP	C		; ∆≠≥ÿÆ∏º¿ ±¿≤ ∂ﬁ ø… œØ∏Ω ¶ ∫¥√≤Ÿ∂
	JR	C,CVDAT2        ; C ∂ﬁ µµ∑π⁄ ﬁ ERROR
	
	LD	A,C
	JP 	CVDAT3
	
CVDAT2:
	
	LD      (REEROR),A	; SET ERROR FLAG
	
CVDAT3:
	POP	HL
	LD	DE,5
	ADD	HL,DE
	POP	DE
	
	LD	(DE),A
	INC	DE
	POP	BC
	DJNZ	CVDAT1
	
	LD	BC,220
	ADD	HL,BC
	POP	BC
	DJNZ	CVDAT0
	
	LD	A,(REEROR)
	OR	A
	RET	Z
	
	LD	HL,PARAM
	LD	B,10
	CALL 	PFILE
	
	RET
	
CVR1:
	PUSH	BC
	CALL	REDATA
	LD	A,E
	POP	BC
	RET
	
REEROR:
	DB	0
	
; ***   PRINT VOICE FILE   ***
	
VFILE:
	
	CALL	RAMI
	
	PUSH	HL
	
	LD	A,(ONSHOKU)
	INC 	A
	
	LD 	L,A
	LD 	H,0
	ADD	HL,HL
	ADD	HL,HL
	ADD	HL,HL
	ADD	HL,HL
	ADD	HL,HL    	; A*32
	
	LD	DE,-6
	ADD	HL,DE
	
	LD	DE,OTODAT
	ADD	HL,DE
	
	LD	A,(HL)
	OR	A
	JR	Z,VFI2
	
	LD	DE,FONTS
	LD	BC,6
	LDIR
	
	LD	HL,FONTS
	LD	DE,0*256+63
	CALL	DISPLAY
	JP	VFI3
	
VFI2:
	LD	HL,MSG003
	LD	DE,0*256+50
	CALL	DISPLAY
	
VFI3:
	
	CALL    ROMI
	
	POP 	HL
	RET
	
FONTS:
	DB 	0,0,0,0,0,0,0,0,0,0
	
; ***   OCTAVE MOVE   ***
	
OCTVDOWN:
	
	PUSH HL
	LD HL,CTRL_BUF
	BIT 5,A
	JR NZ,EXIT
	CALL KEY_CHECK
	POP HL
	RET C
	
	DEC (HL)
	LD	A,(HL)
	
	CP	0FFH
	JR	NZ,OCTVD2
	
	LD	A,7
	LD	(HL),A
	
OCTVD2:
	CALL	OCTP
	RET
	
	
OCTVUP:
	PUSH 	HL
	LD 	HL,SHIFT_BUF
	BIT 	3,A
	JR 	NZ,EXIT
	CALL 	KEY_CHECK
	POP 	HL
	RET 	C
	
	INC 	(HL)
	LD	A,(HL)
	
	CP	8
	JR	NZ,OCTVU2
	
	XOR	A
	LD	(HL),A
	
OCTVU2:
	CALL	OCTP
	RET
	
EXIT:
	
	XOR 	A
	LD 	(HL),A
	POP 	HL
	RET
	
; ---   µ∏¿∞Ãﬁ ¶ Ãﬂÿ›ƒ   ---
	
OCTP:
	INC	A
	LD	L,A
	LD	H,0
	CALL	HEXDEC
	LD	DE,4
	ADD	HL,DE
	LD	DE,010BH
	CALL	DISPLAY
	RET
	
	
; ***   VOICE LOAD & DOWN & UP   ***
	
LOADF:
	CALL	ROMI
	LD	HL,MSG027
	CALL	WINDOW1
	CALL	GETSUJI
	JR	NC,LF4
	LD	HL,6020H-6
	LD	B,255
	CALL	RAMI
LF2:
	LD	DE,INPBUF
	PUSH	BC
	PUSH	HL
	CALL	MCMP
	POP	HL
	POP	BC
	JR	NC,LF3	;FIND
	LD	DE,32
	ADD	HL,DE
	INC	C
	DJNZ	LF2
	JR	LOADF
LF3:
	LD	L,C
LF4:
	LD	A,L
	LD	HL,ONSHOKU
	LD	(HL),A
	PUSH	HL
	CALL	W1CLS
	POP	HL
	JP	OTODW3
	
OTODOWN:
	PUSH 	HL
	LD 	HL,PF1_BUF
	BIT 	1,A
	JR 	NZ,EXIT
	CALL 	KEY_CHECK
	POP 	HL
	RET 	C
	
	PUSH	HL
	DEC	(HL)
	LD	A,(HL)
	CALL	GETADR
	PUSH	HL
	POP	IX
	
	POP	HL
	LD	DE,-32
	CALL	RAMI
OTODW2:
	LD	A,(IX)  	; µ›ºÆ∏ ∂∏…≥ Ã◊∏ﬁ
	OR	A
	JR	NZ,OTODW3
	DEC	(HL)
	ADD	IX,DE
	JP	OTODW2
	
OTODW3:
	CALL	ROMI
	
	PUSH 	HL
	LD 	L,(HL)
	LD 	H,0
	CALL 	HEXDEC       ;  DIPLAY PRINT
	LD 	DE,VOICELOC  ;
	CALL 	DISPLAY      ;
	POP 	HL           ;
	
	PUSH 	HL
	CALL 	STENV
	POP 	HL
	
	CALL 	VFILE
	CALL 	OTOPST
	CALL	PHEXAREA
	RET
	
OTOUP:
	PUSH 	HL
	LD 	HL,PF2_BUF
	BIT 	0,A
	JP 	NZ,EXIT
	CALL 	KEY_CHECK
	POP 	HL
	RET 	C
	
	PUSH	HL
	INC	(HL)
	LD	A,(HL)
	CALL	GETADR
	PUSH	HL
	POP	IX
	
	POP	HL
	LD	DE,32
	CALL	RAMI
OTOUP2:
	LD	A,(IX)  	; µ›ºÆ∏ ∂∏…≥ Ã◊∏ﬁ
	OR	A
	JR	NZ,OTOUP3
	INC	(HL)
	ADD	IX,DE
	JP	OTOUP2
	
OTOUP3:
	CALL	ROMI
	PUSH 	HL
	LD 	L,(HL)
	LD 	H,0
	CALL 	HEXDEC       ;  DIPLAY PRINT
	LD 	DE,VOICELOC  ;
	CALL 	DISPLAY	     ;
	POP 	HL           ;
	
	PUSH 	HL
	CALL 	STENV
	POP 	HL
	
	CALL 	VFILE
	CALL	OTOPST
	CALL	PHEXAREA
	
	RET
	
PHEXAREA:
	LD	HL,MSG009
	LD	DE,1*256+25
	CALL	DISPLAY
	LD	DE,(OTOADR)
	LD	HL,42*256+2
	CALL	HEXER
	RET
	
; ***   KEY ENTRY CHECK   ***
	
;  	IN: A <= KEY DATA
;           HL<= BUFFER ADR
;
	
KEY_CHECK:
	CPL
	OR	A
	CPL
	JR	NZ,KEYC00
	LD	(HL),0FFH
	RET		; SET Z FLAG(≈∆” µª⁄√≤≈≤)
KEYC00:
	LD	(KEYC0+1),A
	LD	DE,(KTIME)
	LD	A,D
	OR	E
KEYC0:
	LD	A,0
	JR      Z,KEYC1
	
	LD	D,A
	LD	A,(HL)
	CP	D
	LD	A,D
	JR	Z,KEYEND
KEYC1:
	LD	(HL),A
	
	PUSH	AF
	PUSH	HL
	LD	HL,01200H
KEYC2:
	DEC	HL
	LD	A,H
	OR	L
	JR	NZ,KEYC2
	
	POP	HL
	POP	AF
	AND	A
	
	RET
	
KEYEND:  		; ∑∞ ∆≠≥ÿÆ∏ ¶ ≥π¬π≈≤
	LD	DE,(KTIME)
	DEC	DE
	LD	(KTIME),DE
	
	SCF		; ERROR SIGN
	RET             ; RET TO MAIN
	
	
KTIME:
	DW	500
	
	
KEYSTOP:
	PUSH	AF
	PUSH	BC
	LD	B,0CH
	LD	C,0
KST2:
	IN	A,(C)
	INC	A
	JR	NZ,KST4
KST3:
	INC	C
	DJNZ	KST2
	
	LD	BC,1000
	LD	(KTIME),BC
	
	POP	BC
	POP	AF
	EI
	RET
KST4:
	LD	A,C
	CP	0AH
	JR	NZ,KST5
	IN	A,(C)
	OR	10000000B
	INC	A
	JR	Z,KST3
KST5:
	POP	BC
	POP	AF
	EI
	RET
	
	
; ---   KEY BUFFER   ---
	
CTRL_BUF:DB	0
SHIFT_BUF:DB	0
PF1_BUF:DB	0
PF2_BUF:DB 	0
CURBUF:	DB	0
TEN0BUF:DB	0
TEN1BUF:DB	0
HBUF:	DB	0
ROLLBUF:DB	0
	
; ***   BIT CHECK   ***
	
BITCHECK:
	
	
	LD D,0
	
	BIT 0,A
	JR Z,GETCODE
	INC D
	
	BIT 1,A
	JR Z,GETCODE
	INC D
	
	BIT 2,A
	JR Z,GETCODE
	INC D
	
	BIT 3,A
	JR Z,GETCODE
	INC D
	
	BIT 4,A
	JR Z,GETCODE
	INC D
	
	BIT 5,A
	JR Z,GETCODE
	INC D
	
	BIT 6,A
	JR Z,GETCODE
	INC D
	
	BIT 7,A
	JR Z,GETCODE
	INC D
	
GETCODE:
	
	LD HL,KEYDATA
	LD A,C		; HL=PORT NUMBER
	SUB 2
	
	ADD	A,A
	ADD	A,A
	ADD	A,A	; *8
	ADD	A,D	; + CODE
	
	ADD	A,L
	LD	L,A
	ADC	A,H
	SUB	L
	LD	H,A
	
	LD	A,(HL)	; GET KEY CODE
	LD	IX,CH1DAT
	LD	C,A
	LD	B,A
	LD	A,(OCTAVE)
	
	PUSH	BC
	CALL	GETC3
	POP	BC
	
	IN	A,(0AH)
	BIT	7,A
	JP	NZ,START
	
	LD	A,(OCTAVE)
	PUSH	AF
	LD	A,B
	ADD	A,4
	LD	C,A
	CP	12
	JR	C,GETC12
	SUB	12
	LD	C,A
	POP	AF
	INC	A
	PUSH	AF
GETC12:
	POP	AF
	LD	IX,CH2DAT
	PUSH	BC
	CALL	GETC3
	POP	BC
	
	LD	A,(OCTAVE)
	PUSH	AF
	LD	A,B
	ADD	A,7
	LD	C,A
	CP	12
	JR	C,GETC13
	SUB	12
	LD	C,A
	POP	AF
	INC	A
	PUSH	AF
GETC13:
	POP	AF
	LD	IX,CH3DAT
	CALL	GETC3
	
	JP	START
	
GETC3:
	SLA A
	SLA A
	SLA A
	SLA A
	OR C		; A=OCTAVE & KEY CODE
	
	LD C,A          ;  STORE
	AND 70H         ;  GET BLOCK DATA
	SRL A           ;  A4-A6 Œﬂ∞ƒ º≠¬ÿÆ∏÷≥ ∆ ±‹æŸ
	LD B,A

	LD A,C          ;  RESTORE A
	AND 0FH         ;  GET KEY CODE (C,C+,D ... B)

	ADD A,A
	LD E,A
	LD D,0
	LD HL,FNUMB
	ADD HL,DE

	LD A,(HL)       ;  GET FNUM2
	OR B            ;  A= KEY CODE & FNUM HI
	LD C,A
	INC HL
	LD A,(HL)       ;  GET FNUM1

	LD H,C
	LD L,A

	LD E,H          ;  BLOCK/F-NUMBER2 DATA

FPORT:
	LD A,0A4H       ;  PORT A4H
	ADD A,(IX)
	LD D,A
	CALL PSGOUT

	SUB 4
	LD D,A
	LD E,L          ;  F-NUMBER1 DATA

FMSUB7:
	CALL PSGOUT
	CALL KEYON
	
	RET
	
FNUMB:	                ;  º≠≥ Ω≥(FM) … √ﬁ∞¿

	DW 6A02H,8F02H,0B602H,0DF02H,0B03H,3903H
	DW 6A03H,9E03H,0D503H,1004H,4E04H,8F04H
	DW 0,0          ;  ±∑
	
CH1DAT:
	DB	0
CH2DAT:
	DB	1
CH3DAT:
	DB	2
	
; ***	KEY-ON  ROUTINE   ***

KEYON:
	LD	A,0F0H		; ¡¨›»Ÿ 1
	ADD	A,(IX)
	LD	E,A
	LD	D,28H
	CALL	PSGOUT     	;  KEY-ON
	RET


; ***	µ›ºÆ∏ æØƒ ªÃﬁŸ∞¡› (FM)   ***

STENV:
	CALL	RAMI
	
	PUSH HL

	LD	A,(EDITFG)
	OR	A
	JR	NZ,STEV2
	
	LD 	A,(ONSHOKU)     ;  ‹∞∏ ∂◊ µ›ºÆ∏ ≈› ﬁ∞ ¶ ¥Ÿ
STENV0:
	CALL	GETADR
	LD	(OTOADR),HL
	INC	HL      ;  HL   µ›ºÆ∏√ﬁ∞¿ ∂∏…≥ ±ƒﬁ⁄Ω
	JR	STEV21
STEV2:
	LD	HL,OTOWK+1
STEV21:
	PUSH	HL
	LD	IX,CH1DAT
	CALL	STENV1
	POP	HL
	PUSH	HL
	LD	IX,CH2DAT
	CALL	STENV1
	POP	HL
	LD	IX,CH3DAT
	CALL	STENV1
	
	POP HL
	CALL	ROMI
	RET

STENV1:
	LD 	BC,0406H     ;  4 OPERATER
	                     ;  6 PARAMATER(Det/Mul,Total,KS/AR,DR,SR,SL/RR)
	LD 	A,30H        ;  START=PORT 30H
	ADD	A,(IX)
	LD 	D,A

STENV2:
	PUSH 	BC
STENV3:
	LD E,(HL)       ;  GET DATA
	CALL PSGOUT

	INC D           ;
	INC D           ;  SKIP BLANK PORT
	INC D           ;
	INC D           ;
	INC HL

	DJNZ STENV3

	POP BC
	DEC C
	JP NZ,STENV2

	
	LD A,(HL)	;  GET FEEDBACK/ALGORIZM
	LD E,A
	AND 07H	;  GET ALGORIZM
	LD (ALGOLIZM),A	;  STORE ALGORIZM
	LD A,0B0H	;  GET ALGO SET ADDRES
	ADD	A,(IX)
	
	LD	D,A
	CALL PSGOUT
	RET


	
; ***   PRINT DISPLAY MASSAGE   ***
	
;	IN: HL<=MASSAGE ADR
;           DE<= Y,X
;	BREAK: AF,HL,BC,DE
	
DISPLAY:
	
	CALL RAMI
	
	PUSH	HL
	
	LD	HL,0
	LD	A,D
	OR	A
	JR	Z,DISP3
	
	LD      B,D
	LD	A,E
	LD	DE,120
DISP2:
	ADD	HL,DE
	DJNZ	DISP2
	
	LD	E,A
	LD	D,0
DISP3:
	ADD	HL,DE
	EX	DE,HL
	LD	HL,0F3C8H
	ADD	HL,DE
	EX	DE,HL
	
	POP	HL
	
DISP4:
	LD	A,(HL)
	OR	A
	JR	Z,DISP5
	CP	1		; CODE OF SPACE
	JR	NZ,DISP6
	
	INC	HL
	LD	B,(HL)
	DEC	B
	LD	A,020H
DISP7:
	LD	(DE),A
	INC	DE
	DJNZ	DISP7
	
DISP6:
	LD	(DE),A
	INC	DE
	INC	HL
	
	JP	DISP4
	
DISP5:
	CALL	ROMI
	RET
	
; 	IN:B<= MAX LINE
	
DISPBOX:
	
	PUSH	BC
	PUSH	DE
	
	CALL	DISPLAY
	
	INC	HL
	POP	DE
	INC	D
	POP	BC
	DJNZ	DISPBOX
	
	RET
	
	
; ***   PRINT HEX DATA   ***
	
;	IN:DE<=DATA:HL<=LOCATE X(1-80),Y(1-25)
	
HEXER:
	PUSH	IY
	
	PUSH	HL
	
	LD	IY,MOJIHLD
	
	LD	A,D
	AND	11110000B
	RRCA
	RRCA
	RRCA
	RRCA
	CALL	HEXER2
	LD	A,D
	AND	00001111B
	CALL	HEXER2
	LD      A,E
	AND	11110000B
	RRCA
	RRCA
	RRCA
	RRCA
	CALL	HEXER2
	LD	A,E
	AND	00001111B
	CALL	HEXER2
	
	POP	HL
	CALL	429DH
	LD	DE,MOJIHLD
	EX	DE,HL
	LD	BC,4
	LDIR
	
	POP	IY
	
	RET
	
	
HEXER2:
	LD	HL,MOJIHEX
	ADD	A,L
	LD	L,A
	ADC	A,H
	SUB	L
	LD	H,A
	
	LD	A,(HL)
	LD	(IY),A
	INC	IY
	RET
	
	
MOJIHEX:
	DB	'0','1','2','3','4','5','6','7'
	DB	'8','9','A','B','C','D','E','F'
	
MOJIHLD:
	DB	0,0,0,0
	
	
;************** LINE ROUTINE *******************
;*** x1=(HL+0),y1=(HL+2),x2=(HL+4),y2=(HL+6) ***
;*** c.reg=bank ********************************
	
LINE:
	DI
     	OUT	(C),A			;blane
	LD	E,(HL)
	INC	HL
	LD	D,(HL)
	INC	HL
	PUSH	DE
	LD	B,(HL)			;y1
	INC	HL
	INC	HL
;
	LD	E,(HL)
	INC	HL
	LD	D,(HL)			;x2
	INC	HL
	LD	C,(HL)			;y2
	POP	HL			;x1
	CALL	LINE0
	OUT	(5FH),A
	EI
	RET
;
;**** hl=x1,de=x2,b=y1,c=y2,(color)=color
;
LINE0:	PUSH	HL
	AND	A
	SBC	HL,DE
	POP	HL
	JR	NC,LINE1
;
	EX	DE,HL			;exchange x1,x2
	LD	A,B
	LD	B,C			;exchange y1,y2
	LD	C,A

LINE1:	LD	A,C
	PUSH	DE
;*****************
	EXX
	POP	BC
	LD	H,0
	LD	L,A
	LD	D,H
	LD	E,L
	ADD	HL,HL
	ADD	HL,HL
	ADD	HL,DE
	ADD	HL,HL
	ADD	HL,HL
	ADD	HL,HL
	ADD	HL,HL			;*80
	LD	DE,0C000H
	ADD	HL,DE			;top adrs
;
	LD	A,C
	SRL	B
	RR	C
	SRL	B
	RR	C
	SRL	C			;bc/8
	ADD	HL,BC			;hl=destination adrs.

	AND	00000111B
	LD	IX,MSKDAT
	LD	(LINE11+2),A
LINE11:	LD	E,(IX)
	LD	BC,80
	EXX
;*****************
;
	AND	A
	SBC	HL,DE			;x1-x2
;
	LD	A,B
	SUB	C
	LD	B,0			;y1-y2
	JR	C,LINE3
;
	LD	C,A
;
	PUSH	HL
	AND	A
	SBC	HL,BC			;chk (x1-x2)>(y1-y2)
	POP	HL
	JR	C,LINE2
;
	LD	D,H
	LD	E,L
	SRL	H
	RR	L
	PUSH	DE
	POP	IX
	INC	IX
;
LLINE1:	EXX				;point
	LD	A,E
	OR	(HL)
	LD	(HL),A
	RRC	E
	JP	NC,LINE_1
	INC	HL
LINE_1:	EXX

	DEC	IX
	DB	0DDH
	LD	A,H			;LD	A,XH
	DB	0DDH
	OR	L			;OR	A,XL
	RET	Z

	AND	A
	SBC	HL,BC
	JR	NC,LLINE1
;
	ADD	HL,DE
	EXX
	ADD	HL,BC
	EXX
	JP	LLINE1
;
LINE2:	EX	DE,HL
	LD	H,B
	LD	L,C
	SRL	H
	RR	L
	PUSH	BC
	POP	IX
	INC	IX
;
LLINE2:	EXX				;point
	LD	A,E
	OR	(HL)
	LD	(HL),A
	ADD	HL,BC
	EXX

	DEC	IX
	DB	0DDH
	LD	A,H
	DB	0DDH
	OR	L
	RET	Z

	AND	A
	SBC	HL,DE
	JR	NC,LLINE2
;
	ADD	HL,BC
	EXX
	RRC	E
	JP	NC,LINE_2
	INC	HL
LINE_2:	EXX
	JP	LLINE2
;
LINE3:	NEG
	LD	C,A
;
	PUSH	HL
	AND	A
	SBC	HL,BC
	POP	HL
	JR	C,LINE4
;
	LD	D,H
	LD	E,L
	SRL	H
	RR	L
	PUSH	DE
	POP	IX
	INC	IX
;
LLINE3:	EXX				;point
	LD	A,E
	OR	(HL)
	LD	(HL),A
	RRC	E
	JP	NC,LINE_3
	INC	HL
LINE_3:	EXX

	DEC	IX
	DB	0DDH
	LD	A,H
	DB	0DDH
	OR	L
	RET	Z
	
	AND	A
	SBC	HL,BC
	JR	NC,LLINE3
	
	ADD	HL,DE
	EXX
	AND	A
	SBC	HL,BC
	EXX
	JP	LLINE3
;
LINE4:	EX	DE,HL
	LD	H,B
	LD	L,C
	SRL	H
	RR	L
	PUSH	BC
	POP	IX
	INC	IX
;
LLINE4:	EXX				;point
	LD	A,E
	OR	(HL)
	LD	(HL),A
	AND	A
	SBC	HL,BC
	EXX

	DEC	IX
	DB	0DDH
	LD	A,H
	DB	0DDH
	OR	L
	RET	Z
;
	AND	A
	SBC	HL,DE
	JR	NC,LLINE4
;
	ADD	HL,BC
	EXX
	RRC	E
	JP	NC,LINE_4
	INC	HL
LINE_4:	EXX
	JP	LLINE4
;
MSKDAT:	DB	128,64,32,16,8,4,2,1
	
	
; ***	TEXT CLS	***
	
CLS:
	LD	HL,0F3C8H
	LD	DE,0F3C9H
	LD	(HL),0
	LD	BC,79
	LDIR
	LD	B,1
	CALL	71C6H
	
; ***	VRAM CLS	***
	
	CALL	6EE6H
	RET
	
BACK:
	DI
	OUT	(5CH),A
	LD	HL,0C000H
	LD	DE,0C001H
	LD	B,100
BACK2:
	PUSH	BC
	LD	BC,80
	LD	(HL),10001000B
	LDIR
	LD	(HL),00100010B
	LD	BC,80
	LDIR
	POP	BC
	DJNZ	BACK2
	
	OUT	(5FH),A
	EI
	RET
	
	
; ***	WINDOW 1 ∆ “Øæ∞ºﬁ ¶ ÀÆ≥ºﬁΩŸ	***
;	IN:	HL<= MESSAGE DATA ADR
	
WINDOW1:
	LD	DE,9*256+56
	CALL	DISPLAY
	RET
	
W1CLS:
	PUSH	AF
	LD	HL,MSG008
	CALL	WINDOW1
	POP	AF
	RET
	
; ***	WINDOW 2 ∆ “Øæ∞ºﬁ ¶ ÀÆ≥ºﬁΩŸ	***
;	IN:	HL<= MESSAGE DATA ADR
	
WINDOW2:
	PUSH	HL
	LD	HL,MSG030
	LD	DE,15*256+54
	LD	B,9
	CALL	DISPBOX
	POP	HL
	
	LD	DE,16*256+55
	LD	B,7
	CALL	DISPBOX
	RET
	
; ***	LINE INPUT ∆ ÷Ÿ 1 BYTE √≤Ω≥ … ÷–∫–	***
	
;	EXIT:	HL<= 1BYTE √≤Ω≥ … DATA
;	STOP:	SCF
	
GETSUJI:
	CALL	LININP
	JR	C,GETS2
	LD	HL,INPBUF
	CALL	REDATA
	JR	C,GETS3
	OR	A
	JR	NZ,GETS3
	EX	DE,HL
GETS1:
	PUSH	HL
	CALL	W1CLS
	POP	HL
	LD	DE,0101H
	LD	(CURSOR),DE
	AND	A
	RET
GETS2:
	LD	DE,0101H
	LD	(CURSOR),DE
	CALL	W1CLS
	POP	DE		; DUMMY
	RET			; TO MAIN
GETS3:
	LD	HL,0
	CALL	GETS1
	SCF		; ”ºﬁ⁄¬√ﬁ∞¿¿ﬁ
	RET
	
; ***   SYSTEM WORK AREA   ***
	
VIEW1BUF:
	DW	0,17,639,17
	DB	05DH
VIEW2BUF:
	DW	292,17,292,199
	DB	05DH
	DW	291,17,291,199
	DB	05DH
	
NUMBUF:
	DB	0,0,0,0,0,0,0,0,0,0
ALGOLIZM:
	
	DB	0		; ±Ÿ∫ﬁÿΩﬁ— √ﬁ∞¿
ONSHOKU:
	
	DB	90		; πﬁ›ªﬁ≤ … µ›ºÆ∏ √ﬁ∞¿
OCTAVE:
	DB	4		; µ∏¿∞Ãﬁ
	
KEYDATA:
	
; --------	PORT 02		--------
	
	DB	0
	DB	0
	DB	07H	; O5 G
	DB	04H	; O5 E
	DB	03H	; O5 D+
	DB	0
	DB	0
	DB	06H	; O5 F+
	
; --------	PORT 03		--------
	
	DB	08H	; O5 G+
	DB	0
	DB	0AH	; O5 A+
	DB	0
	DB	0
	DB	0BH	; O5 B
	DB	09H	; O5 A
	DB	0
	
; --------	PORT 04		--------
	
	DB	0
	DB	0
	DB	0
	DB	01H	; O5 C+
	DB	0
	DB	0
	DB	05H	; O5 F
	DB	0
	
; --------	PORT 05		--------
	
	DB	02H	; O5 D
	DB	0
	DB	00H	; O5 C
	DB	0
	DB	0
	DB	0
	DB	0
	DB	0
	
; ***   MESSAGE  DATA   ***
	
MSG001:
	DB	'* TONE EDITOR Ver:1.0 *  Voice number = ',0
MSG002:
	DB	' Voice file "      "',0
MSG003:
	DB	' Voice file "NO_USE"',0
MSG004:
	DB	'OP1  OP2  OP3  OP4 ->MAX   ENVELOPE         ALGORHYTM',0
MSG005:
	DB	'ATACK  .R',1,21,'0-031',0
	DB	'DECAY  .R',1,21,'0-031',0
	DB	'SUSTIN .R',1,21,'0-031',0
	DB	'REREASE.R',1,21,'0-015',0
	DB	'SUSTIN .L',1,21,'0-015',0
	DB	'TOTAL  .L',1,21,'0-127',0
	DB	'KEYSC  .R',1,21,'0-003',0
	DB	'MULTIPLE ',1,21,'0-015',0
	DB	'DETUNE   ',1,21,'0-007',0
	DB	'FB/AL    ',1,21,'0-007',0
MSG006:
	DB	' Octave  =',0
MSG007:
	DB	'<<<   Edit mode   >>>',0
MSG008:
	DB	1,22,0
	DB	1,22,0
	DB	1,22,0
	DB	1,22,0
	
MSG009:
	DB	'Address      = $',0
MSG010:
	DB	'[1ïïï2ïïï3ïïï4ïïï>',0
	DB	' ',0
	DB	' ',0
	DB	' ',0
MSG011:
	DB	'[1ïïï3ïïï4ïïï>',0
	DB	'     ñ',0
	DB	' 2ïïïõ',0
	DB	' ',0
MSG012:
	DB	' 2ïïï3ïïï4ïïï>',0
	DB	'     ñ',0
	DB	'[1ïïïõ',0
	DB	' ',0
MSG013:
	DB	'[1ïïï2ïïï4ïïï>',0
	DB	'     ñ',0
	DB	' 3ïïïõ',0
	DB	' ',0
MSG014:
	DB	'[1ïïï2ïïïô',0
	DB	'         ìïïï>',0
	DB	' 3ïïï4ïïïõ',0
	DB	' ',0
MSG015:
	DB	'     òïïï2ïïïô',0
	DB	'[1ïïïèïïï3ïïïèïïï>',0
	DB	'     öïïï4ïïïõ',0
	DB	' ',0
MSG016:
	DB	'[1ïïï2ïïïô',0
	DB	'     3ïïïèïïï>',0
	DB	'     4ïïïõ',0
	DB	' ',0
MSG017:
	DB	'[1ïïïô',0
	DB	' 2ïïïí',0
	DB	' 3ïïïèïïï>',0
	DB	' 4ïïïõ',0
	
MSG018:
	DB	1,22,0
	DB	'E :√ﬁ∞¿¶ ±∑Ãß≤ŸÕ ƒ≥€∏ ',0
	DB	'Q :¥√ﬁ®Øƒ … º≠≥ÿÆ≥    ',0
	DB	'R :√ﬁ∞¿¶ ”ƒ…Ãß≤ŸÕ ƒ≥€∏',0
	DB	'B :1¬ œ¥ … √ﬁ∞¿ ∆ ”ƒﬁΩ',0
	DB	1,22,0
	DB	1,22,0
	
MSG019:
	DB	'Used voice 000-',0
MSG020:
	DB	'Tone name? "',1,6,'"     ',0
MSG021:
	DB	'No.    ∆ ƒ≥€∏ºœº¿',1,5,0
MSG022:
	DB	'CTRL+',1,17,0
	DB	1,4,'L :LOAD    TONE',1,3,0
	DB	1,4,'D :DELETE  TONE',1,3,0
	DB	1,4,'E :EDIT    TONE',1,3,0
	DB	1,4,'S :SYSTEM',1,9,0
	DB	1,4,'Q :QUIT EDIT',1,6,0
	DB	1,22,0
MSG024:
	DB	1,22,0
	DB	1,4,'S :SAVE FILE',1,6,0
	DB	1,4,'L :LOAD FILE',1,6,0
	DB	1,22,0
	DB	1,22,0
	DB	1,22,0
	DB	1,22,0
	
MSG025:
	DB	'Now Saving...',1,4,0
MSG026:
	DB	'Now Loading...',1,3,0
MSG027:
	DB	'Load No.?  "',1,6,'"',1,5,0
MSG028:
	DB	'Delete No.?"',1,6,'"',1,5,0
MSG029:
	DB	'SET No.?   "',1,6,'"',1,5,0
MSG030:
	DB	9CH,95H,95H,95H,95H,95H
	DB	'   COMMAND   '
	DB	95H,95H,95H,95H,95H
	DB	9DH,0
	DB	96H,1,23,96H,0
	DB	96H,1,23,96H,0
	DB	96H,1,23,96H,0
	DB	96H,1,23,96H,0
	DB	96H,1,23,96H,0
	DB	96H,1,23,96H,0
	DB	96H,1,23,96H,0
	DB	9EH,95H,95H,95H,95H
	DB	95H,95H,95H,95H,95H
	DB	95H,95H,95H,95H,95H
	DB	95H,95H,95H,95H,95H
	DB	95H,95H,95H,95H
	DB	9FH,0
	
MSG031:
	DB	'OP1',0
MSG032:
	DB	'OP2',0
MSG033:
	DB	'OP3',0
MSG034:
	DB	'OP4',0
MSG035:
	DB	'** HELP (EXIT:ESC) **',0
	DB	'ROLLUP  :TONE +',0
	DB	'ROLLDOWN:TONE -',0
	DB	'+',1,7,':OCTAVE +',0
	DB	'-',1,7,':OCTAVE -',0
	DB	'CAPS',1,4,':CODE',0
	DB	1,22,0
MSG040:
	DB	'TENKEY  :DATASET',0
	DB	'ROLLUP  :DATA +',0
	DB	'ROLLDOWN:DATA -',0
	DB	'ESC',1,5,':QUIT  ',0
	DB	1,22,0
	DB	1,22,0
	DB	1,22,0
MSG041:
	DB	'***',0
	
; ***   SYSTEM WORK AREA   ***
	
OTOADR:
	DW	0
LOCATE:
	DW 	0
	
;PARAM:
	;DB 	0,0,0,0		; AR
	;DB 	0,0,0,0         ; DR
	;DB 	0,0,0,0         ; SR
	;DB	0,0,0,0         ; RR
	;DB	0,0,0,0         ; SL
	;DB	0,0,0,0		; OL
	;DB	0,0,0,0		; KS
	;DB	0,0,0,0		; ML
	;DB	0,0,0,0		; DT
	;DB	0,0,0,0         ; FB/AR
	
EDITFG:
	DB	0
INPCO:
	DB	0
